<!DOCTYPE html>
<html lang="en">
    <head>
        <meta charset="UTF-8" />
        <meta name="viewport" content="width=device-width, initial-scale=1.0" />
        <title>skarja.la</title>
        <link rel="stylesheet" href="styles.css" />
        <script src="webgl.js" type="module"></script>
    </head>
    <body>
        <div class="frontpage-content-container">
            <div class="frontpage-container">
                <div class="header-container">
                    <div class="header-title">
                        <a href=".." class="invisilink">
                            <div class="header-name">
                                skarjala
                            </div>
                        </a>
                        <div class="header-spacer">â€¢</div>
                        <div class="header-contact">
                            spencerkarjala@gmail.com
                        </div>
                    </div>
                    <hr style="width:100%; height:0;" />
                    <div class="nav-container">
                        <a class="nav-item" href="../about.html">About</a>
                        <a class="nav-item" href="../projects.html">Projects</a>
                    </div>
                </div>
            </div>
        </div>
        <canvas id="glcanvas" class="front-page-background-webgl" width='100' height="100"></canvas>
        <svg id="front-page-background-noise" class="front-page-background-noise" viewBox="0 0 100 100" xmlns="http://www.w3.org/2000/svg">
            <filter id="noiseFilter">
                <feTurbulence type="fractalNoise" baseFrequency="15.0" numOctaves="3" stitchTiles="stitch" />
                <feComponentTransfer>
                    <feFuncA type="linear" slope="0.3"/>
                </feComponentTransfer>
            </filter>
            <rect width="100%" height="100%" filter="url(#noiseFilter)" />
        </svg>
    </body>
    <script id="vertex" type="x-shader/x-vertex">
        attribute vec4 a_position;
        attribute vec4 a_color;
        attribute vec4 a_barycentric;
        
        uniform mat4 u_matrix;
        
        varying vec4 v_color;
        
        uniform float u_time;
        uniform float u_side_length;
        uniform vec2 u_resolution;
        attribute vec4 a_offset;

        varying vec3 v_barycentric;
        varying vec3 v_position;
        varying float v_time;
        varying float v_time_index;
        varying vec2 v_resolution_frag;
        
        mat4 make_translation(in vec3 offset) {
            return mat4(
                1, 0, 0, 0,
                0, 1, 0, 0,
                0, 0, 1, 0,
                offset.x, offset.y, offset.z, 1
            );
        }
        
        mat4 make_x_rotation(in float angle_rad) {
            float cos_angle = cos(angle_rad);
            float sin_angle = sin(angle_rad);
        
            return mat4(
                1, 0,         0,         0,
                0, cos_angle, sin_angle, 0,
                0,-sin_angle, cos_angle, 0,
                0, 0,         0,         1
            );
        }
        
        mat4 make_y_rotation(in float angle_rad) {
            float cos_angle = cos(angle_rad);
            float sin_angle = sin(angle_rad);
        
            return mat4(
                cos_angle, 0,-sin_angle, 0,
                0,         1, 0,         0,
                sin_angle, 0, cos_angle, 0,
                0,         0, 0,         1
            );
        }
        
        mat4 make_z_rotation(in float angle_rad) {
            float cos_angle = cos(angle_rad);
            float sin_angle = sin(angle_rad);
        
            return mat4(
                 cos_angle, sin_angle, 0, 0,
                -sin_angle, cos_angle, 0, 0,
                0,          0,         1, 0,
                0,          0,         0, 1
            );
        }
        
        mat4 make_projection(in float width, in float height, in float depth) {
            return mat4(
                2.0 / width, 0, 0, 0,
                0, -2.0 / height, 0, 0,
                0, 0, 2.0 / depth, 0,
                -1, 1, 0, 1
            );
        }

        mat4 make_scale(in vec3 dims) {
            return mat4(
                dims.x, 0, 0, 0,
                0, dims.y, 0, 0,
                0, 0, dims.z, 0,
                0, 0, 0, 1
            );
        }

        vec4 apply_stretch_effect(in vec4 position) {
            float factor = 1.0 + abs(pow(position.x, 3.0));
            return vec4(factor*position.x, position.y, position.z, position.w);
        }
        
        void main() {
            float dist = (a_offset.x + a_offset.y) * -0.2;
            float time_scaled = (u_time + dist) / 8.0;
            float time_frac = time_scaled - floor(time_scaled);
        
            float t = time_frac;
            v_time = (time_frac < 0.5)
                ? 4.0*t*t*t
                : 1.0 - pow(-2.0*t + 2.0, 3.0) / 2.0;

            const float PI = 3.1415926535;
            const float PI2 = 2.0 * PI;

            v_time_index = time_scaled - time_frac;
            int anim_index = int(mod(v_time_index, 4.0));
            float x_rotation;
            float y_rotation;
            if (anim_index == 0) {
                x_rotation = PI * (4.0 -  8.0*v_time)/12.0;
                y_rotation = PI * (0.0 - 12.0*v_time)/12.0;
            }
            else if (anim_index == 1) {
                x_rotation = PI * (-4.0 + 12.0*v_time)/12.0;
                y_rotation = PI * ( 12.0 - 4.0*v_time)/12.0;
            }
            else if (anim_index == 2) {
                x_rotation = PI * (8.0 + 12.0*v_time)/12.0;
                y_rotation = PI * (8.0 -  4.0*v_time)/12.0;
            }
            else if (anim_index == 3) {
                x_rotation = PI * (20.0 - 16.0*v_time)/12.0;
                y_rotation = PI * ( 4.0 -  4.0*v_time)/12.0;
            }

            //vec3 offset_adjusted = vec3(a_offset.x, a_offset.y/sqrt(sqrt(2.0)), a_offset.z);
            vec3 offset_adjusted = vec3(a_offset.x, a_offset.y, a_offset.z);

            mat4 transformation = u_matrix
                * make_translation(offset_adjusted.xyz * 2.0*u_side_length)
                * make_y_rotation(y_rotation)
                * make_x_rotation(x_rotation)
                * make_translation(-a_offset.xyz * 2.0*u_side_length);

            gl_Position = apply_stretch_effect(transformation * a_position);
            v_color = a_color;
            v_barycentric = a_barycentric.xyz;
            v_resolution_frag = u_resolution;
        }
    </script>
    
    <script id="fragment" type="x-shader/x-fragment">
        #extension GL_OES_standard_derivatives : enable
        precision mediump float;

        varying vec3 v_barycentric;
        varying vec4 v_color;
        varying float v_time;
        varying float v_time_index;
        varying vec2 v_resolution_frag;

        // some help from https://catlikecoding.com/unity/tutorials/advanced-rendering/flat-and-wireframe-shading/
        float wireframe() {
            vec3 bary_derivative = fwidth(v_barycentric);
            vec3 antialiased_wire = smoothstep(0.0 * bary_derivative, 1.0 * bary_derivative, v_barycentric);
            antialiased_wire = antialiased_wire * antialiased_wire * antialiased_wire;
            return min(min(antialiased_wire.x, antialiased_wire.y), antialiased_wire.z);
        }

        const vec3 color0 = vec3(0.384, 0.412, 0.416);
        const vec3 color1 = vec3(0.871, 0.839, 0.871);
        const vec3 color2 = vec3(0.835, 0.745, 0.804);
        const vec3 color3 = vec3(0.514, 0.569, 0.545);

        vec3 get_color() {
            vec3 output_color;
            int anim_index = int(mod(v_time_index, 4.0));
            if (anim_index == 0) {
                output_color = mix(color1, color2, v_time);
            }
            else if (anim_index == 1) {
                output_color = mix(color2, color0, v_time);
            }
            else if (anim_index == 2) {
                output_color = mix(color0, color3, v_time);
            }
            else {
                output_color = mix(color3, color1, v_time);
            }
            return output_color;
        }
        
        void main() {
            float signed_dist = abs(wireframe());
            if (signed_dist < 1.0) {
                vec3 color = get_color();
                gl_FragColor = vec4(color.r, color.g, color.b, 1.0) * (1.0 - signed_dist);
            }
        }
    </script>
</html>
