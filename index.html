<!DOCTYPE html>
<html lang="en">
    <head>
        <meta charset="UTF-8" />
        <meta name="viewport" content="width=device-width, initial-scale=1.0" />
        <title>skarja.la</title>
        <link rel="stylesheet" href="styles.css" />
        <script src="webgl.js" type="module"></script>
    </head>
    <body>
        <!-- <div class="frontpage-container">
            skarjala
        </div> -->
        <canvas id="glcanvas" width='100' height="100" style="object-fit:contain;"></canvas>
    </body>
    <script id="vertex" type="x-shader/x-vertex">
        attribute vec4 a_position;
        attribute vec4 a_color;
        attribute vec4 a_barycentric;
        
        uniform mat4 u_matrix;
        
        varying vec4 v_color;
        
        uniform float u_time;
        uniform float u_side_length;
        attribute vec4 a_offset;

        varying vec3 v_barycentric;
        varying vec3 v_position;
        
        mat4 make_translation(in vec3 offset) {
            return mat4(
                1, 0, 0, 0,
                0, 1, 0, 0,
                0, 0, 1, 0,
                offset.x, offset.y, offset.z, 1
            );
        }
        
        mat4 make_x_rotation(in float angle_rad) {
            float cos_angle = cos(angle_rad);
            float sin_angle = sin(angle_rad);
        
            return mat4(
                1, 0,         0,         0,
                0, cos_angle, sin_angle, 0,
                0,-sin_angle, cos_angle, 0,
                0, 0,         0,         1
            );
        }
        
        mat4 make_y_rotation(in float angle_rad) {
            float cos_angle = cos(angle_rad);
            float sin_angle = sin(angle_rad);
        
            return mat4(
                cos_angle, 0,-sin_angle, 0,
                0,         1, 0,         0,
                sin_angle, 0, cos_angle, 0,
                0,         0, 0,         1
            );
        }
        
        mat4 make_z_rotation(in float angle_rad) {
            float cos_angle = cos(angle_rad);
            float sin_angle = sin(angle_rad);
        
            return mat4(
                 cos_angle, sin_angle, 0, 0,
                -sin_angle, cos_angle, 0, 0,
                0,          0,         1, 0,
                0,          0,         0, 1
            );
        }
        
        mat4 make_projection(in float width, in float height, in float depth) {
            return mat4(
                2.0 / width, 0, 0, 0,
                0, -2.0 / height, 0, 0,
                0, 0, 2.0 / depth, 0,
                -1, 1, 0, 1
            );
        }

        mat4 make_scale(in vec3 dims) {
            return mat4(
                dims.x, 0, 0, 0,
                0, dims.y, 0, 0,
                0, 0, dims.z, 0,
                0, 0, 0, 1
            );
        }

        vec4 apply_stretch_effect(in vec4 position) {
            //float factor = 1.0 + pow(10.0, -abs(position.x));
            float factor = 1.0 + abs(pow(position.x, 3.0));
            return vec4(factor*position.x, position.y, position.z, position.w);
        }
        
        void main() {
            float dist = (a_offset.x + a_offset.y) * -0.1;
            float time_scaled = (u_time + dist) / 8.0;
            float time_frac = time_scaled - floor(time_scaled);
        
            float t = time_frac;
            float time_eased = (time_frac < 0.5)
                ? 4.0*t*t*t
                : 1.0 - pow(-2.0*t + 2.0, 5.0) / 2.0;

            const float PI = 3.1415926535;
            const float PI2 = 2.0 * PI;

            float time_trunc = time_scaled - time_frac;
            int anim_index = int(mod(time_trunc, 4.0));
            float x_rotation;
            float y_rotation;
            if (anim_index == 0) {
                x_rotation = PI * (4.0 -  8.0*time_eased)/12.0;
                y_rotation = PI * (0.0 - 12.0*time_eased)/12.0;
            }
            else if (anim_index == 1) {
                x_rotation = PI * (-4.0 + 12.0*time_eased)/12.0;
                y_rotation = PI * ( 12.0 - 4.0*time_eased)/12.0;
            }
            else if (anim_index == 2) {
                x_rotation = PI * (8.0 + 12.0*time_eased)/12.0;
                y_rotation = PI * (8.0 -  4.0*time_eased)/12.0;
            }
            else if (anim_index == 3) {
                x_rotation = PI * (20.0 - 16.0*time_eased)/12.0;
                y_rotation = PI * ( 4.0 -  4.0*time_eased)/12.0;
            }

            vec3 offset_adjusted = vec3(a_offset.x, a_offset.y/sqrt(sqrt(2.0)), a_offset.z);

            mat4 transformation = u_matrix
                * make_translation(offset_adjusted.xyz * 2.0*u_side_length)
                * make_y_rotation(y_rotation)
                * make_x_rotation(x_rotation)
                * make_translation(-a_offset.xyz * 2.0*u_side_length);

            gl_Position = apply_stretch_effect(transformation * a_position);
            v_color = a_color;
            v_barycentric = a_barycentric.xyz;
        }
    </script>
    
    <script id="fragment" type="x-shader/x-fragment">
        #extension GL_OES_standard_derivatives : enable
        precision mediump float;

        varying vec3 v_barycentric;
        varying vec4 v_color;

        float edge() {
            vec3 d = fwidth(v_barycentric);
            vec3 f = step(d * 1.0, v_barycentric);
            return min(min(f.x, f.y), f.z);
        }
        
        // got barycentric working with some help from https://tchayen.github.io/posts/wireframes-with-barycentric-coordinates
        void main() {
            float signed_dist = min(min(v_barycentric.x, v_barycentric.y), v_barycentric.z);
            signed_dist = edge();

            if (abs(signed_dist) < 0.01) {
                gl_FragColor = v_color;
            }
            else {
                gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
            }
        }
    </script>
</html>
